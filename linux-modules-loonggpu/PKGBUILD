# Maintainer: zint <zint@tuta.io>
# Contributor: zint <zint@tuta.io>

pkgname=linux-modules-loonggpu
pkgver=1.0.2.lnd25.1.rc1.7.r63.g2576e56
pkgrel=1
pkgdesc="LOONGGPU binary kernel module with AOSC patch"
license=()
arch=(loong64)
makedepends=(
	gcc14
	)
depends=(
	linux-firmware-loongson
	)
options=(!debug !strip)
source=(
	"$pkgname::git+https://github.com/AOSC-Tracking/loonggpu-kernel-dkms.git#branch=aosc/v1.0.2-lnd25.1-rc1.7"
)
b2sums=('SKIP')
install="loonggpu.install"

pkgver() {
	cd "${srcdir}/${pkgname}"
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
	cd "${srcdir}/${pkgname}"
	make CC=gcc-14 IGNORE_CC_MISMATCH=1 LG_VERBOSE=1 -j${nproc} modules
}
package(){
	cd "${srcdir}/${pkgname}"

	ZSTD_CLEVEL=19 make INSTALL_MOD_PATH="${pkgdir}/usr" INSTALL_MOD_STRIP=1 DEPMOD=/doesnt/exist modules_install

	install -d -m755 "${pkgdir}/etc/udev/rules.d"
	printf 'KERNEL=="kcd", GROUP="video", MODE="0660"\n' > \
		"${pkgdir}/etc/udev/rules.d/70-loonggpu.rules"
	chmod 644 "${pkgdir}/etc/udev/rules.d/70-loonggpu.rules"
}
